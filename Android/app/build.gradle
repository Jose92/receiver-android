/*
 * Copyright (C) 2019 Intel Corporation
 *
 * SPDX-License-Identifier: Apache-2.0
 *
 */
apply plugin: 'com.android.application'
android {
    def versionMajor = 3
    def versionMinor = 5
    def versionPatch = 0
    compileSdk 34

    defaultConfig {
        applicationId "org.opendroneid.android"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            versionNameSuffix ".debug"
            resValue "string", "app_version", "${defaultConfig.versionName}${versionNameSuffix}"
        }

        release {
            resValue "string", "app_version", "${defaultConfig.versionName}"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions += "version"
    productFlavors {
        create("gplay") {
            dimension = "version"
        }
        create("foss") {
            dimension = "version"
        }
    }
    compileOptions {
        sourceCompatibility = 11
        targetCompatibility = 11
    }
    namespace 'org.opendroneid.android'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    def support_version = "30.0.0"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.mikepenz:fastadapter:3.3.1'
    implementation 'com.mikepenz:fastadapter-commons:3.2.8'
    implementation 'com.android.support:recyclerview-v7:' + support_version
    implementation 'com.android.support:appcompat-v7:' + support_version
    implementation 'com.android.support:support-fragment:' + support_version
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'pub.devrel:easypermissions:3.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'

    gplayImplementation 'com.google.android.gms:play-services-maps:19.0.0'
    gplayImplementation 'com.google.android.gms:play-services-location:21.3.0'
    fossImplementation 'androidx.preference:preference:1.2.1'
    fossImplementation 'org.osmdroid:osmdroid-android:6.1.13'
}
